[properties]
namespace: "Giraffe.Frontend"

[grammar]
# === Terminals ===
kw_properties -> "[properties]" ;
kw_grammar -> "[grammar]" ;
kw_entry -> "[entry]" ;
term_name -> /[a-z][a-z0-9_]*/ ;
nonterm_name -> /[A-Z][A-Z0-9_]*/ ;
arrow -> "->" ;
end -> ";" ;
regex -> /\/(\\\/|[^\/])+\// ;
string -> /"(\\\\|\\"|[^"])+"/ ;
expand -> ".." ;
discard -> "_" ;
colon -> ":" ;
ws _ -> /\s+/ ;
comment _ -> /#.*\n/ ;

# === Nonterminals ===
FILE [entry] -> _kw_properties PROPERTIES _kw_grammar GRAMMAR ;

PROPERTIES -> ..PROPERTY_DEF_T ;
PROPERTY_DEF -> term_name _colon string ;
PROPERTY_DEF_T -> PROPERTY_DEF ..PROPERTY_DEF_T
               -> ;

GRAMMAR -> ANY_DEF ..ANY_DEF_T ;
ANY_DEF -> TERM_DEF
        -> NONTERM_DEF ;
ANY_DEF_T -> ANY_DEF ..ANY_DEF_T
          -> ;
TERM_DEF -> term_name OPT_DISCARD _arrow TERM_RHS _end ;
TERM_RHS -> regex ;
TERM_RHS -> string ;
NONTERM_DEF -> nonterm_name OPT_KW_ENTRY RULE ..RULE_T _end ;
RULE -> _arrow ..SYMBOLS ;
RULE_T -> RULE ..RULE_T
       -> ;
SYMBOLS -> SYMBOL ..SYMBOLS
        -> ;
SYMBOL -> OPT_DISCARD ..SYMBOL_T ;
SYMBOL_T -> term_name ;
SYMBOL_T -> OPT_EXPAND nonterm_name ;

OPT_KW_ENTRY -> kw_entry
             -> ;
OPT_EXPAND -> expand
           -> ;
OPT_DISCARD -> discard
            -> ;
